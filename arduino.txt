#include <SoftwareSerial.h>

SoftwareSerial BTSerial(2, 3); // RX, TX

const int redPin = 9;
const int greenPin = 10;
const int bluePin = 11;

int r = 0, g = 0, b = 0;
bool isOn = false;

void setup() {
  Serial.begin(9600);      
  BTSerial.begin(9600);    

  pinMode(redPin, OUTPUT);
  pinMode(greenPin, OUTPUT);
  pinMode(bluePin, OUTPUT);

  setColor(0, 0, 0);
}

void loop() {
  static String input = "";

  while (BTSerial.available()) {
    char c = BTSerial.read();
    if (c == '\n') {
      parseInput(input);
      input = "";
    } else {
      input += c;
    }
  }
}

void parseInput(String data) {
  int values[4];
  int index = 0;
  int lastComma = -1;

  for (int i = 0; i < data.length(); i++) {
    if (data.charAt(i) == ',' || i == data.length() - 1) {
      int endIndex = (data.charAt(i) == ',') ? i : i + 1;
      String numStr = data.substring(lastComma + 1, endIndex);
      values[index++] = numStr.toInt();
      lastComma = i;
      if (index >= 4) break;
    }
  }

  if (index == 4) {
    r = values[0];
    g = values[1];
    b = values[2];
    isOn = (values[3] == 1);
    updateLED();
  }
}

void updateLED() {
  if (isOn) {
    setColor(r, g, b);
  } else {
    setColor(0, 0, 0);
  }
}

void setColor(int r, int g, int b) {
  analogWrite(redPin, r);
  analogWrite(greenPin, g);
  analogWrite(bluePin, b);
}
